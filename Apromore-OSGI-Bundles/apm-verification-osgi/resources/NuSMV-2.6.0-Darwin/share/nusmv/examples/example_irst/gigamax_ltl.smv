MODULE cache-device

VAR
  state : {invalid,shared,owned};


DEFINE
  readable := ((state = shared) | (state = owned)) & !waiting;
  writable := (state = owned) & !waiting;

ASSIGN
  init(state) := invalid;
  next(state) :=
    case
      abort : state;
      master :
        case
          CMD = read-shared        : shared;
          CMD = read-owned         : owned;
          CMD = write-invalid      : invalid;
          CMD = write-resp-invalid : invalid;
          CMD = write-shared       : shared;
          CMD = write-resp-shared  : shared;
          TRUE : state;
        esac;
      !master & state = shared & (CMD = read-owned | CMD = invalidate) :
         invalid;
      state = shared : {shared,invalid};
      TRUE : state;
    esac;

DEFINE
  reply-owned := !master & state = owned;

VAR
  snoop : {invalid,owned,shared};

ASSIGN
  init(snoop) := invalid;
  next(snoop) := 
    case
      abort : snoop;
      !master & state = owned & CMD = read-shared : shared;
      !master & state = owned & CMD = read-shared : owned;
      master & CMD = write-resp-invalid : invalid;
      master & CMD = write-resp-shared  : invalid;
      TRUE : snoop;
    esac;

MODULE bus-device

VAR
  master  : boolean;
  cmd : {idle,read-shared,read-owned,write-invalid,write-shared,
	 write-resp-invalid,write-resp-shared,invalidate,response};
  waiting : boolean;
  reply-stall : boolean;

ASSIGN
  init(waiting) := FALSE;
  next(waiting) :=
    case
      abort : bool(waiting);
      master  & CMD = read-shared        : TRUE;
      master  & CMD = read-owned         : TRUE;
      !master & CMD = response           : FALSE;
      !master & CMD = write-resp-invalid : FALSE;
      !master & CMD = write-resp-shared  : FALSE;
      TRUE : bool(waiting);
    esac;

DEFINE
  reply-waiting := !master & waiting;
  abort := REPLY-STALL
           | ((CMD = read-shared | CMD = read-owned) & REPLY-WAITING);

MODULE processor(CMD,REPLY-OWNED,REPLY-WAITING,REPLY-STALL)
ISA bus-device
ISA cache-device

ASSIGN
  cmd :=
    case
      master & state = invalid : {read-shared,read-owned};
      master & state = shared  : read-owned;
      master & state = owned & snoop = owned : write-resp-invalid;
      master & state = owned & snoop = shared : write-resp-shared;
      master & state = owned & snoop = invalid : write-invalid;
      TRUE : idle;
    esac;

MODULE memory(CMD,REPLY-OWNED,REPLY-WAITING,REPLY-STALL)
VAR
  master : boolean;
  cmd : {idle,read-shared,read-owned,write-invalid,write-shared,
	 write-resp-invalid,write-resp-shared,invalidate,response};
  busy : boolean;
  reply-stall : boolean;


DEFINE
  reply-owned := 0;
  reply-waiting := 0;
  abort := REPLY-STALL
          | (CMD = read-shared | CMD = read-owned) & REPLY-WAITING
	  | (CMD = read-shared | CMD = read-owned) & REPLY-OWNED;

ASSIGN
  init(busy) := FALSE;
  next(busy) :=
    case
      abort : busy;
      master & CMD = response : FALSE;
      !master & (CMD = read-owned | CMD = read-shared) : TRUE;
      TRUE : busy;
    esac;
  cmd :=
    case
      master & busy : {response,idle};
      TRUE : idle;
    esac;
  reply-stall :=
    case
      busy & (CMD = read-shared | CMD = read-owned
	      | CMD = write-invalid | CMD = write-shared
	      | CMD = write-resp-invalid | CMD = write-resp-shared) : TRUE;
      TRUE : {FALSE,TRUE};
    esac;

MODULE main
VAR
  CMD : {idle,read-shared,read-owned,write-invalid,write-shared,
	   write-resp-invalid,write-resp-shared,invalidate,response};
  p0 : processor(CMD,REPLY-OWNED,REPLY-WAITING,REPLY-STALL);
  p1 : processor(CMD,REPLY-OWNED,REPLY-WAITING,REPLY-STALL);
  p2 : processor(CMD,REPLY-OWNED,REPLY-WAITING,REPLY-STALL);
  m : memory(CMD,REPLY-OWNED,REPLY-WAITING,REPLY-STALL);

DEFINE
  REPLY-OWNED := p0.reply-owned | p1.reply-owned | p2.reply-owned;
  REPLY-WAITING := p0.reply-waiting | p1.reply-waiting | p2.reply-waiting;
  REPLY-STALL := p0.reply-stall | p1.reply-stall | p2.reply-stall |
	         m.reply-stall;

ASSIGN
  CMD := 
    case
      p1.cmd = idle & p2.cmd = idle & m.cmd = idle : p0.cmd;
      p0.cmd = idle & p2.cmd = idle & m.cmd = idle : p1.cmd;
      p0.cmd = idle & p1.cmd = idle & m.cmd = idle : p0.cmd;
      p0.cmd = idle & p1.cmd = idle & p2.cmd = idle : m.cmd;
      TRUE : {idle,read-shared,read-owned,write-invalid,write-shared,
	   write-resp-invalid,write-resp-shared,invalidate,response};
    esac;

ASSIGN
  p0.master := {FALSE,TRUE};
  p1.master :=
    case
      p0.master : FALSE;
      TRUE : {FALSE,TRUE};
    esac;
  p2.master :=
    case
      p0.master | p1.master : FALSE;
      TRUE : {FALSE,TRUE};
    esac;
  m.master :=
    case
      p0.master | p1.master | p2.master : FALSE;
      TRUE : {FALSE,TRUE};
    esac;

SPEC
  AG EF (p0.readable)
SPEC
AG EF (p0.writable)

LTLSPEC  G !(p0.writable & p1.writable)
LTLSPEC  !G (p0.writable & p1.writable)
LTLSPEC  G (p0.writable & p1.writable)
-- VAR
--   LTL_2_SPECF_1 : boolean; 
-- TRANS
--   next(LTL_2_SPECF_0) = LTL_2_SPECF_1 
-- DEFINE
--    LTL_2_SPECF_0 := (LTL_2_SPECF_2|(( 1 )&LTL_2_SPECF_1)) ;
--    LTL_2_SPECF_2 := (LTL_2_SPECF_3|LTL_2_SPECF_4) ;
--    LTL_2_SPECF_4 := (!( p1.writable )) ;
--    LTL_2_SPECF_3 := (!( p0.writable )) ;
-- FAIRNESS
--    (!LTL_2_SPECF_0)|LTL_2_SPECF_2 
-- SPEC
--    !(LTL_2_SPECF_0 & EG 1)

-- LTLSPEC G !p0.abort

